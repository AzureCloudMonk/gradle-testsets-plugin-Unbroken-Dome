plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7'
    id 'com.gradle.plugin-publish' version '0.9.6'
}

targetCompatibility = 1.7


repositories {
    jcenter()
}


sourceSets {
    integrationTest
}


configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}


dependencies {
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'

    testRuntimeOnly 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.21'
}


gradlePlugin {
    testSourceSets sourceSets.integrationTest
}


task integrationTest(type: Test) {
    dependsOn pluginUnderTestMetadata
    group = 'verification'
    description = 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}


apply from: "$rootDir/publishing.gradle"
